ext {
	bulldogVersion = '0.1.1'
	distributionDirectory = new File('../dist', 'Version-' + bulldogVersion);
}

def boardDistributionDirectory(String boardName) {
	return new File(distributionDirectory, boardName)
}

def javaProjects = [
					project(":bulldog.core"), 
					project(":bulldog.devices"), 
					project(":bulldog.examples"), 
					project(":bulldog.linux"),
				   ]
				   
def boardProjects = [
					 project(":bulldog.board.beagleboneblack"),
					 project(":bulldog.board.raspberrypi")
					]

def nativeProjects = [
					  project(":bulldog.linux.native")
					 ]
					 
					 
def allJavaProjects = new LinkedList(javaProjects);
allJavaProjects.addAll(boardProjects)

		
if(hasProperty('board')) {
	println "building for " + board
	boardProjects = boardProjects.findAll { it.path.contains(board) };
} 

configure(nativeProjects) {
	apply from: new File(project.projectDir, 'build.gradle')
	version = bulldogVersion
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}
					 
configure(allJavaProjects) { 
   	   
    apply plugin: 'java'
    apply plugin: 'eclipse'
   	
    version = bulldogVersion
 
    repositories {
       mavenCentral()
    }
 
    dependencies {
    	testCompile 'junit:junit:4.8.2'
    }
 
    jar {
        manifest.attributes 'Implementation-Version': version
        manifest.attributes provider: 'Datenheld'
        from ("${projectDir}/src/main/java/") {
        	include('*/**/*.dts.*')
    	}
    }
    
    task sourcesJar(type: Jar, dependsOn:classes) { 
       classifier = 'sources' 
       from sourceSets.main.allSource 
	} 

	task javadocJar(type: Jar, dependsOn:javadoc) { 
	     classifier = 'javadoc' 
	     from javadoc.destinationDir 
	} 
	
	artifacts { 
	     archives sourcesJar 
	     archives javadocJar 
	} 
    
    uploadArchives {
    	repositories {
	    	 flatDir {
	        	dirs 'bulldog.repository'
	    	}
    	}
    }	
}

boardProjects.each { def boardProject -> 
	evaluationDependsOn(boardProject.path) 
	boardProject.distributions.each { distribution ->
    	tasks.create(name: "singleJar$distribution.distributionBaseName", type: Jar) { 
	        baseName = distribution.distributionBaseName
	        destinationDir = boardDistributionDirectory(distribution.distributionBaseName)
	        
	        manifest { 
	     		attributes 'Main-Class': 'org.bulldog.examples.App' 
	     		attributes 'Created-By': 'libbulldog.org - Version ' + bulldogVersion
	     	}
	    
	    	javaProjects.each { javaProject -> 
	    		evaluationDependsOn(javaProject.path) 
	    		from zipTree(javaProject.jar.archivePath) 
			}	
			
			from zipTree(boardProject.jar.archivePath)
		}
    } 
} 

boardProjects.each { def boardProject -> 
	evaluationDependsOn(boardProject.path)
	evaluationDependsOn(project(':bulldog.linux.native').path)
	boardProject.distributions.each { distribution ->
	    tasks.create(name: "distribute$distribution.distributionBaseName", type: Copy, dependsOn: [ project(':bulldog.linux.native').tasks.matching  { Task task -> task.name.contains('BulldogSharedLibrary') } ]) { 
	       //delete project.distributionDirectory
	       destinationDir = boardDistributionDirectory(distribution.distributionBaseName)
	       destinationDir.mkdirs()
	       FileCollection collection = files(distribution.distributionFiles)
	       from collection
	       into destinationDir
       }
    }     
} 

boardProjects.each { def boardProject -> 
	evaluationDependsOn(boardProject.path)
	boardProject.distributions.each { distribution ->
	    tasks.create(name: "createZipArchive$distribution.distributionBaseName", type: Zip) { 
	       baseName = distribution.distributionBaseName + '-' + bulldogVersion + "-" + getDate()
	       destinationDir = boardDistributionDirectory(distribution.distributionBaseName)
	       excludes = [ '*.zip', '*sources.*' ]
	       from boardDistributionDirectory(distribution.distributionBaseName)
	       from 'src/main/resources'
	    } 
    }
} 

task docs(type: Javadoc) {
    source allJavaProjects.collect {project -> project.sourceSets.main.allJava } 
    classpath = files(allJavaProjects.collect {project -> project.sourceSets.main.compileClasspath}) 
    destinationDir = new File(distributionDirectory, 'javadoc')
}

task packageBoardArtifacts(dependsOn: [ tasks.matching { Task task -> task.name.startsWith("singleJar")}, subprojects.assemble]) 
task distribution(dependsOn: [ tasks.matching { Task task -> task.name.startsWith("distribute")}, packageBoardArtifacts])
task archiveDistributions(dependsOn: [ tasks.matching { Task task -> task.name.startsWith("createZipArchive")}, distribution])







